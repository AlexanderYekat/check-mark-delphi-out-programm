функция ОтправитьЗапросПроверкиМаркировкиРР(HTTPСоединение, IPСервисаРР, ПортСервисаРР, МаркировочныйКод)
	Результат = ИнициализироватьСтруктуруОтветаПроверкиМарки(МаркировочныйКод);
	
	// Формируем JSON запроса
	ЗапросJSON = Новый Структура;
	ЗапросJSON.Вставить("action", "check");
	ЗапросJSON.Вставить("type", "receipt");
	
	МассивПозиций = Новый Массив;
	СтруктураПозиции = Новый Структура;
	МассивКодов = Новый Массив;
	МассивКодов.Добавить(МаркировочныйКод);
	СтруктураПозиции.Вставить("marking_codes", МассивКодов);
	//структураОрганизации = Новый Структура;
	//структураОрганизации.Вставить("inn", "123");
	//СтруктураПозиции.Вставить("organization", структураОрганизации);
	МассивПозиций.Добавить(СтруктураПозиции);
	
	ЗапросJSON.Вставить("positions", МассивПозиций);
	
	// Преобразуем структуру в строку JSON
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	ЗаписатьJSON(ЗаписьJSON, ЗапросJSON);
	ТелоЗапроса = ЗаписьJSON.Закрыть();
	
	//Сообщить("Запуск провекри марки");
	
	Попытка
		// Формируем HTTP запрос
		Если HTTPСоединение = Неопределено Тогда
			HTTPСоединение = Новый HTTPСоединение(IPСервисаРР, ПортСервисаРР);
		КонецЕсли;
		//HTTP = Новый HTTPСоединение(IPСервисаРР, ПортСервисаРР);
		
		// Заголовки запроса
		Заголовки = Новый Соответствие;
		Заголовки.Вставить("Connection", "keep-alive"); // Держим соединение открытым
		Заголовки.Вставить("Content-Type", "application/json");
		
		// Отправляем POST запрос
		HTTPЗапрос = Новый HTTPЗапрос("/document", Заголовки);
		HTTPЗапрос.УстановитьТелоИзСтроки(ТелоЗапроса);
		
		ОтветHTTP = HTTPСоединение.ОтправитьДляОбработки(HTTPЗапрос);
		
		// Читаем ответ
		ТелоОтвета = ОтветHTTP.ПолучитьТелоКакСтроку();
		
		// Преобразуем ответ из JSON
		ЧистыйJSON = СтрЗаменить(ТелоОтвета, """fmu-api-offline""", """fmuApiOffline""");
		ЧистыйJSON = СтрЗаменить(ЧистыйJSON, """fmu-api-localModul""", """fmuApiLocalModul""");
		
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.УстановитьСтроку(ЧистыйJSON);
		ОтветJSON = ПрочитатьJSON(ЧтениеJSON);
		
		// Заполняем основные поля результата
		Если ОтветJSON.Свойство("Code") Тогда
			Результат.CodeFMU = ОтветJSON.Code;
		КонецЕсли;
		
		Если ОтветJSON.Свойство("Error") Тогда
			Результат.DescrErrorFMU = УбратьСпецСимволы(ОтветJSON.Error);
		КонецЕсли;
		
		// Получаем данные из truemark_response
		Если ОтветJSON.Свойство("truemark_response") Тогда
			TruemarkResponse = ОтветJSON.truemark_response;
			
			Если TruemarkResponse.Свойство("code") Тогда
				Результат.CodeCHZMAK = TruemarkResponse.code;
			КонецЕсли;			
			
			Если TruemarkResponse.Свойство("description") Тогда
				Результат.DescrErrorCHZMAK = TruemarkResponse.description;
			КонецЕсли;
			
			Если TruemarkResponse.Свойство("reqId") Тогда
				UUID = TruemarkResponse.reqId;				
				ПозInst = СтрНайти(UUID, "&Inst");
				Если ПозInst > 0 тогда
					UUID = Лев(UUID, ПозInst - 1);
				конецЕсли;
				Результат.UUID = UUID;
			КонецЕсли;
			
			Если TruemarkResponse.Свойство("reqTimestamp") Тогда
				Результат.TimeStamp = Формат(TruemarkResponse.reqTimestamp, "ЧГ=");
			КонецЕсли;
			
			Если TruemarkResponse.Свойство("inst") Тогда
				Результат.Inst = TruemarkResponse.inst;
			КонецЕсли;
			
			Если TruemarkResponse.Свойство("version") Тогда
				Результат.Ver = TruemarkResponse.version;
			КонецЕсли;			
		КонецЕсли;
		
	Исключение
		Результат.DescrErrorFMU = ОписаниеОшибки();
		//Сообщить("Ощитбка проверки марки");
		//Сообщить(ОписаниеОшибки());
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции //ОтправитьЗапросПроверкиМаркировкиРР
